<?xml version="1.0" encoding="utf-8"?>
<events:EventDispatcher xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:events="flash.events.*" xmlns:ns="AfcsNameSpace" xmlns:adapt="bombs.adapt.*" xmlns:shared="bombs.model.shared.*" xmlns:model="bombs.model.*" xmlns:util="bombs.util.*">

	<mx:Script>
		<![CDATA[
			import bombs.model.Bomb;
			import bombs.util.SimpleListEvent;
			import com.adobe.rtc.sharedModel.SharedProperty;
			import com.adobe.rtc.events.SharedPropertyEvent;
			import bombs.model.Character;
			import mx.events.CollectionEventKind;
			import mx.events.CollectionEvent;
			
			// CONSTS // 
			public static const ROOM:String = "http://connectnow.acrobat.com/seanhessnet/game2";
			public static const USER:String = "seanhess@gmail.com";
			public static const PASS:String = "cocomo";
			
			[Bindable] public var playing:Boolean = false;
			[Bindable] public var mainCharacter:Character;
			
			public function connect():void
			{
				session.login();
				characters.subscribe();
				bombsList.subscribe();
			}
			
			public function nuke():void
			{
				characters.removeAll();
				bombsList.removeAll();
			}
			
			public function joinGame(character:Character):void
			{
				mainCharacter = character;
				playing = true;
				update();
			}
			
			public function startPlaying():void
			{
				playing = true;
			}
			
			public function move(x:int, y:int):void
			{
				mainCharacter.destinationX = x;
				mainCharacter.destinationY = y;
				update();	
			}
			
			/**
			 * updates the main char on the server
			 */
			public function update():void
			{
				characters.share(mainCharacter);
			}
			
			public function lay():void
			{
				var bomb:Bomb = new Bomb();
					bomb.locationX = mainCharacter.locationX;
					bomb.locationY = mainCharacter.locationY;
					
				bombsList.share(bomb);
			}
			
			protected function updateChar(event:SimpleListEvent):void
			{
				var char:Character = event.item as Character;
				var update:Object = event.update;
				
				char.destinationX = update.destinationX;
				char.destinationY = update.destinationY;
				
				if (mainCharacter && char.name == mainCharacter.name)
					mainCharacter = char;
			}
			
			protected function updateBomb(event:SimpleListEvent):void
			{
				var bomb:Bomb = event.item as Bomb;
				var update:Object = event.update;
				
				bomb.locationX = update.locationX;
				bomb.locationY = update.locationY;
			}
		]]>
	</mx:Script>

	<!-- Auth -->	
	<ns:AdobeHSAuthenticator id="auth" userName="{USER}" password="{PASS}"/>

	<!-- Session -->
	<ns:ConnectSession id="session" authenticator="{auth}" roomURL="{ROOM}"/>
	
	<!-- Sharing -->
	<util:SimpleSharedList id="characters" 
		session="{session}" 
		idField="name"
		itemType="{Character}"
		sharedID="characters"
		receive="updateChar(event)"
	/>
	
	<util:SimpleSharedList id="bombsList" 
		session="{session}" 
		idField="id"
		itemType="{Bomb}"
		sharedID="bombs"
		receive="updateBomb(event)"
	/>

</events:EventDispatcher>