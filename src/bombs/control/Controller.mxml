<?xml version="1.0" encoding="utf-8"?>
<events:EventDispatcher xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:events="flash.events.*" xmlns:ns="AfcsNameSpace" xmlns:adapt="bombs.adapt.*" xmlns:shared="bombs.model.shared.*" xmlns:model="bombs.model.*" xmlns:util="bombs.util.*">

	<mx:Script>
		<![CDATA[
			import bombs.util.BombTimer;
			import bombs.model.Bomb;
			import bombs.util.SimpleListEvent;
			import com.adobe.rtc.sharedModel.SharedProperty;
			import com.adobe.rtc.events.SharedPropertyEvent;
			import bombs.model.Character;
			import mx.events.CollectionEventKind;
			import mx.events.CollectionEvent;
			
			// CONSTS // 
			public static const ROOM:String = "http://connectnow.acrobat.com/seanhessnet/game2";
			public static const USER:String = "seanhess@gmail.com";
			public static const PASS:String = "cocomo";
			
			public static const CHARACTER_SIZE:int = 20;
			public static const BLAST_RADIUS:int = 20;
			
			[Bindable] public var playing:Boolean = false;
			[Bindable] public var mainCharacter:Character;
			
			public function connect():void
			{
				session.login();
				characters.subscribe();
				bombsList.subscribe();
			}
			
			public function nuke():void
			{
				characters.removeAll();
				bombsList.removeAll();
			}
			
			public function joinGame(character:Character):void
			{
				mainCharacter = character;
				playing = true;
				characters.add(mainCharacter);
			}
			
			public function startPlaying():void
			{
				playing = true;
			}
			
			public function move(x:int, y:int):void
			{
				if (!playing) return;
				
				mainCharacter.destinationX = x;
				mainCharacter.destinationY = y;
				update();	
			}
			
			/**
			 * updates the main char on the server
			 */
			public function update():void
			{
				if (!playing) return;
				characters.update(mainCharacter);
			}
			
			public function lay():void
			{
				if (!playing) return;
								
				if (mainCharacter.bombs < 1)
					return;
				
				mainCharacter.bombs--;
				
				var bomb:Bomb = new Bomb();
					bomb.locationX = mainCharacter.locationX;
					bomb.locationY = mainCharacter.locationY;
					bomb.owner = mainCharacter.name;
					
				bombsList.add(bomb);
			}
			
			protected function updateChar(event:SimpleListEvent):void
			{
				var char:Character = event.item as Character;
				var update:Object = event.update;
				
				if (!mainCharacter || char.name != mainCharacter.name)
				{
					char.destinationX = update.destinationX;
					char.destinationY = update.destinationY;
				}
				
				char.radius = update.radius;
				char.avatar = update.avatar;
				
				if (mainCharacter && char.name == mainCharacter.name)
				{
					char.avatar = mainCharacter.avatar;
					mainCharacter = char;
				}
			}
			
			/**
			 * 
			 */
			protected function updateBomb(event:SimpleListEvent):void
			{
				if (!playing) return;
				
				var bomb:Bomb = event.item as Bomb;
				var update:Object = event.update;
				
				bomb.locationX = update.locationX;
				bomb.locationY = update.locationY;
				bomb.blastRadius = update.blastRadius;
				bomb.fuse = update.fuse;
				bomb.owner = update.owner;
				
				var timer:BombTimer = new BombTimer();
					timer.countDown(bomb);
					timer.addEventListener(BombTimer.EXPLODE, onExplode);
			}

			/**
			 * Removes the bomb from the list, and if you are
			 * the owner, checks to see if you've killed anyone
			 */
			protected function onExplode(event:Event):void
			{
				if (!playing) return;
				
				var timer:BombTimer = event.target as BombTimer;
				timer.removeEventListener(BombTimer.EXPLODE, onExplode);

				var bomb:Bomb = timer.bomb;
				
				if (mainCharacter && bomb.owner == mainCharacter.name)
				{
					for (var name:String in characters.items)
					{
						var char:Character = characters.items[name];
						if (bomb.space.intersects(char.space))
						{
							char.dead = true;
							characters.remove(char);
							mainCharacter.kills++;
						}
					}
					
					mainCharacter.bombs++;
					
					update(); // tell everybody about your kills!
				}
				
				bombsList.remove(bomb); // so, EVERYONE is removing the bombs redundantly
			}
			
			protected function onRemoveChar(event:SimpleListEvent):void
			{
				if (event.item == mainCharacter)
				{
					playing = false;
					mainCharacter = null;
				}
			}
			
		]]>
	</mx:Script>

	<!-- Auth -->	
	<ns:AdobeHSAuthenticator id="auth" userName="{USER}" password="{PASS}"/>

	<!-- Session -->
	<ns:ConnectSession id="session" authenticator="{auth}" roomURL="{ROOM}"/>
	
	<!-- Sharing -->
	<util:SimpleSharedList id="characters" 
		session="{session}" 
		idField="name"
		itemType="{Character}"
		sharedID="characters"
		receive="updateChar(event)"
		remove="onRemoveChar(event)"
	/>
	
	<util:SimpleSharedList id="bombsList" 
		session="{session}" 
		idField="id"
		itemType="{Bomb}"
		sharedID="bombs"
		receive="updateBomb(event)"
	/>

</events:EventDispatcher>